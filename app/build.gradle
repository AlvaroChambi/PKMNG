import PerfTestTaskGeneratorPlugin
apply plugin: 'com.android.application'
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: 'kotlin-kapt'
def version_major = 1
def version_minor = 0
def version_micro = 1

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    buildScan {
        termsOfServiceUrl = "https://gradle.com/terms-of-service"
        termsOfServiceAgree = "yes"
    }

    configurations {
        cleanedAnnotations
        compile.exclude group: 'org.jetbrains' , module:'annotations'
    }

    defaultConfig {
        applicationId "es.developer.achambi.pkmng"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode version_major * 10000 + version_minor * 1000 + version_micro * 100
        versionName version_major + "." + version_minor + "." + version_micro

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    lintOptions {
        disable 'GoogleAppIndexingWarning', 'IconLocation',
                'ButtonStyle', 'ContentDescription',
                'LabelFor', 'GradleDependency'
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "mode"
    productFlavors {
        dev {
            dimension "mode"
        }

        automation {
            dimension "mode"
        }
    }

    sourceSets {
        main {
            java.srcDirs = ['src/main/java', 'src/main/groovy']
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(path: ':coreframework')

    androidTestImplementation('com.android.support.test:runner:0.5') {
        exclude module: 'support-annotations'
    }
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2') {
        exclude module: 'support-annotations'
    }
    androidTestImplementation('com.android.support.test.espresso:espresso-contrib:2.2') {
        // Necessary to avoid version conflicts
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude module: 'recyclerview-v7'
    }

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.6.3'

    kapt "android.arch.persistence.room:compiler:1.1.1"
}

// Create performance testing tasks for all connected Android devices using a Gradle plugin defined
// in the buildSrc directory.
apply plugin: PerfTestTaskGeneratorPlugin
